# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-bgprocess
pkgver=0.1.0
_vdrapi=2.2.0
pkgrel=20
pkgdesc="Displays information about background processes"
url="http://projects.vdr-developer.org/projects/plg-undelete"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://ftp.uni-kl.de/pub/linux/gentoo/distfiles/$pkgname-$pkgver.tgz"
        "$pkgname-Makefile::https://raw.githubusercontent.com/CReimer/vdr-plugin-bgprocess/master/Makefile"
        'bgprocess-tlphrases-fix.diff'
        'bgprocess-osditem-fix.diff')
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('46bc8626cf843110eedef74d2dc27c3d'
         '581d7d18981892f9a3302a12ed7d3b37'
         'd2f7efb1ab40727298d4d17e1b07e37c'
         '1556e199ab90264a1f45838abbef0243')

prepare() {
  cd "${srcdir}/${_plugname}-${pkgver}"

  patch -p1 -i ${srcdir}/bgprocess-tlphrases-fix.diff #By Holger Schvestka (yaVDR)
  patch -p1 -i ${srcdir}/bgprocess-osditem-fix.diff #By Holger Schestka (yaVDR)
  cp "$srcdir/$pkgname-Makefile" Makefile
}

build() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${_plugname}-${pkgver}"

  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
