# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-wirbelscan
pkgver=0.0.7
_vdrapi=2.1.1
pkgrel=2
pkgdesc="Performs a channel scans for DVB-T, DVB-C and DVB-S"
url="http://wirbel.htpc-forum.de/wirbelscan/index2.html"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://wirbel.htpc-forum.de/$_plugname/$pkgname-$pkgver.tgz"
        "vdr-wirbelscan-0.0.7-receiver-api-fixes.patch.diff::http://www.vdr-portal.de/index.php?page=Attachment&attachmentID=32195")
md5sums=('b07d57cecbb0dc3cbf1d13f3d47bc7f3'
         '0acf42568623bec4638e71636ebf7564')

prepare() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  patch -p1 -i "$srcdir/vdr-wirbelscan-0.0.7-receiver-api-fixes.patch.diff"
}

package() {
  cd "${srcdir}/${_plugname}-${pkgver}"

  mkdir -p "$pkgdir/usr/lib/vdr/plugins"
  make CFLAGS="$(pkg-config vdr --variable=cflags)" \
       CXXFLAGS="$(pkg-config vdr --variable=cxxflags)" \
       VDRDIR="/usr/include/vdr" \
       LIBDIR="$pkgdir/$(pkg-config vdr --variable=libdir)" \
       LOCALEDIR="$pkgdir/$(pkg-config vdr --variable=locdir)" \
       all
}
