# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-extrecmenu
pkgver=1.2.2_15_g2131477
epoch=1
_gitver=21314770e74d4d33d811b9a48097ff8d41ef8ae7
_vdrapi=2.1.1
pkgrel=3
pkgdesc="provides additional functions to VDR's recordings menu"
url="http://projects.vdr-developer.org/projects/show/plg-extrecmenu"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
makedepends=('git')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/vdr-plugin-extrecmenu.git#commit=$_gitver"
        'fix_wrong_foldernames_with_nopacity.diff')
md5sums=('SKIP'
         'd00cdbf1bf9ef393f67f98775121f630')

pkgver() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  git describe --tags | sed 's/-/_/g;s/v//'
}

prepare() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  patch -p1 -i "$srcdir/fix_wrong_foldernames_with_nopacity.diff"
}

package() {
  cd "${srcdir}/vdr-plugin-${_plugname}"

  mkdir -p "$pkgdir/usr/lib/vdr/plugins"
  make CFLAGS="$(pkg-config vdr --variable=cflags)" \
       CXXFLAGS="$(pkg-config vdr --variable=cxxflags)" \
       VDRDIR="/usr/include/vdr" \
       LIBDIR="$pkgdir/$(pkg-config vdr --variable=libdir)" \
       LOCALEDIR="$pkgdir/$(pkg-config vdr --variable=locdir)" \
       EXTRECMENU_USE_VDR_CUTTER=1
}
