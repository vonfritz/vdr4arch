# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-dbus2vdr
pkgver=12
pkgrel=3
_vdrapi=2.1.1
pkgdesc="Expose some methods via DBus to control the vdr"
url="https://github.com/flensrocker/vdr-plugin-dbus2vdr"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('dbus' 'glib2' 'libpng' "vdr-api=${_vdrapi}")
makedepends=('png++')
backup=('etc/dbus-1/system.d/de.tvdr.vdr.conf')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("https://github.com/flensrocker/vdr-plugin-dbus2vdr/archive/v12.tar.gz")
md5sums=('201ec3b131860db493a77460ac1452e4')

build() {
  cd "$srcdir/vdr-plugin-$_plugname-$pkgver"
  make
}

package() {
  cd "$srcdir/vdr-plugin-$_plugname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm755 "$pkgdir/usr/share/vdr-plugin-$_plugname/vdr-dbus-send.sh" \
    "$pkgdir/usr/bin/vdr-dbus-send"

  rm -r "$pkgdir/usr/share/vdr-plugin-$_plugname"
  rm -r "$pkgdir/etc/init"
}
