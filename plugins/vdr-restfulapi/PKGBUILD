# Maintainer: Alexander Grothe <seahawk1986[at]hotmail[dot]com>
pkgname=vdr-restfulapi
pkgver=0.2.1.2
pkgrel=2
_vdrapi=2.2.0
pkgdesc="Restful-API Control for VDR"
url="https://github.com/yavdr/vdr-plugin-restfulapi"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('cxxtools' "vdr-api=${_vdrapi}")
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("https://github.com/yavdr/vdr-plugin-$_plugname/archive/$pkgver.tar.gz"
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf"
        'var/lib/vdr/plugins/restfulapi/API.html'
        'var/lib/vdr/plugins/restfulapi/osd.css'
        'var/lib/vdr/plugins/restfulapi/osd.js')
md5sums=('c5b8e5fd301a8a68b44eae456b7c3b4b'
         '7b482249828a58f1f6ce2cd4a577f773')

prepare() {
  cd "$srcdir/vdr-plugin-$_plugname-$pkgver"
  sed -i -e 's/var\/lib\/video\.00/srv\/vdr\/video/' tools.cpp
}

build() {
  cd "$srcdir/vdr-plugin-$_plugname-$pkgver"
  make
}

package() {
  cd "$srcdir/vdr-plugin-$_plugname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -m 644 web/osd.js     "$pkgdir/var/lib/vdr/plugins/restfulapi/"
  install -m 644 web/osd.css    "$pkgdir/var/lib/vdr/plugins/restfulapi/"

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"

  chown -R 666:666 "$pkgdir/var/lib/vdr"
}
