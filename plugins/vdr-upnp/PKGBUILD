# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-upnp
pkgver=1.0.0_14_g11a035a
_gitver=11a035aa4ec2b55faaef1eaa48b7726a28422048
_vdrapi=2.2.0
pkgrel=15
pkgdesc="extends the VDR with the possibility to act as an UPnP/DLNA Media Server (DMS)"
url="http://projects.vdr-developer.org/projects/plg-softhddevice"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('libupnp' 'tntdb' 'tntnet' "vdr-api=${_vdrapi}")
makedepends=('boost' 'git')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/vdr-plugin-$_plugname.git#commit=$_gitver"
        'upnp-vdr2.1.2compat.diff'
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf"
        'var/lib/vdr/plugins/upnp/channelTitle.conf')
md5sums=('SKIP'
         '9aa486e95f113ac1791d5faffde27f5e'
         '145e9e822d1d734c4dcb294db4bb88d4')

pkgver() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  git tag -a v1.0.0 -m 'Added Tag' 529f178bd15a361baf1e83837b1e872be14795a6 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

prepare() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  patch -p1 -i "$srcdir/upnp-vdr2.1.2compat.diff"
}

build() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"

  chown -R 666:666 "$pkgdir/var/lib/vdr"
}
