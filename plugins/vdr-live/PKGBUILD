# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-live
pkgver=0.3.0_12_g8849321
epoch=1
_gitver=88493218652e4196fd5c2a3bd64f496697cb02dc
_vdrapi=2.2.0
pkgrel=7
pkgdesc="Adds the possibility to control VDR and some of it's plugins by a web interface."
url="http://projects.vdr-developer.org/projects/plg-live"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('pcre' 'tntnet' "vdr-api=${_vdrapi}")
optdepends=('vdr-epgsearch: Manage searchtimers'
            'vdr-streamdev: Stream live TV')
makedepends=('git')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://github.com/CReimer/vdr-plugin-live.git#commit=$_gitver"
        'live-folderstatecookie-v2.diff::http://www.vdr-portal.de/index.php?page=Attachment&attachmentID=34407'
        'live-osd-v4.diff'
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('SKIP'
         '6abb3d1724057e765e98e1a9b5326fe3'
         '7aae6abe11ce66be2dcc4b49f3e59f2c'
         '563961eb90d9f2b3d2a0a34472ef51ee')
pkgver() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  git tag -a 0.3.0 -m 'Added Tag' 9f56065e62206700b5ab22a9af4d4a253a714d48 2> /dev/null
   git describe --tags | sed 's/-/_/g;s/v//'
}

prepare() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  patch -p1 -i "$srcdir/live-folderstatecookie-v2.diff"
  patch -p1 -i "$srcdir/live-osd-v4.diff"
}

build() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make DESTDIR="$pkgdir" install install-resources

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
