# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-rssreader
pkgver=2.2.0
_vdrapi=2.2.0
pkgrel=1
pkgdesc="Provides a simple OSD menu based user interface for reading user-defined RSS streams"
url="http://www.saunalahti.fi/rahrenbe/vdr/rssreader/"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('curl' 'expat' "vdr-api=${_vdrapi}")
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://www.saunalahti.fi/~rahrenbe/vdr/$_plugname/files/$pkgname-$pkgver.tgz")
backup=('var/lib/vdr/plugins/rssreader/rssreader.conf'
        "etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('fc011014cdb349152c65e5f078e546f8')

build() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make GITTAG=''
}


package() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"

  chown -R 666:666 "$pkgdir/var/lib/vdr"
}
