# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-radio
pkgver=1.0.0
_vdrapi=2.2.0
pkgrel=16
pkgdesc="Display an background image while the vdr is switched to radio channels"
url="http://www.egal-vdr.de/plugins/"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://egal-vdr.de/plugins/$pkgname-$pkgver.tgz"
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('c654f7ddeb37aa8088b504d7e2be88ce'
         'bbb81b2f18c2ee2d54b501e778c00b01')

build() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make
}
package() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  #Install all stillpictures
  mkdir -p "$pkgdir/usr/share/vdr/plugins/$_plugname"
  install -Dm644 config/mpegstill/* "$pkgdir/usr/share/vdr/plugins/$_plugname"

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
