# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-systeminfo
pkgver=0.1.3
_vdrapi=2.1.1
pkgrel=8
pkgdesc="displays system informations like CPU type and speed, disk and swap space etc."
url="http://firefly.vdr-developer.org/systeminfo/index.html"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('iproute2' 'lm_sensors' 'hddtemp' "vdr-api=${_vdrapi}")
#install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://firefly.vdr-developer.org/systeminfo/${pkgname}-${pkgver}.tar.bz2"
        'systeminfo.sh')
md5sums=('1fe264cf2059628103f134c9aba53529'
         'ea31a11d19b78f30f389793ebc34c3cb')

prepare() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  sed -i 's/usr\/local/usr\/lib\/vdr/g' systeminfo.c
}

build() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm755 ${srcdir}/systeminfo.sh "$pkgdir/usr/lib/vdr/bin/systeminfo.sh"
}
