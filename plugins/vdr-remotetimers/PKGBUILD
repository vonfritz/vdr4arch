# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-remotetimers
pkgver=1.0.1
_vdrapi=2.2.0
pkgrel=9
pkgdesc="Add/Edit/Remove timers on both, the local (client) VDR and a remote (server) VDR"
url="http://vdr.schmirler.de/"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}" 'vdr-svdrpservice')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://vdr.schmirler.de/$_plugname/$pkgname-$pkgver.tgz"
        'remotetimers-vdr2.1.2compat.diff')
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('aece17988ec1d62d3985390eabbcfdc9'
         '6eb43dfefc9da87bc691abf193e2ef12')

prepare() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  patch -p1 -i "$srcdir/remotetimers-vdr2.1.2compat.diff"
}

build() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
