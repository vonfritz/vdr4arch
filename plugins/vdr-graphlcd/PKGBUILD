# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-graphlcd
pkgver=20130127
_gitver=5b5bf594f026fbcba2e31b423b43a2e5e45fd471
_vdrapi=2.1.1
pkgrel=2
pkgdesc="Plugin for the Video Disc Recorder to display its information"
url="http://projects.vdr-developer.org/projects/show/graphlcd"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('graphlcd-base' 'ttf-dejavu' "vdr-api=${_vdrapi}")
optdepends=('serdisplib: Support more modern LCDs')
makedepends=('git')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/vdr-plugin-graphlcd.git#commit=$_gitver")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  git log -1 --pretty=format:%ad --date=short | sed 's/-//g'
}

build() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make DESTDIR="${pkgdir}" install

  cd "$pkgdir/usr/share/vdr/plugins/graphlcd/fonts"
  rm *.ttf
  ln -s /usr/share/fonts/TTF/DejaVuSans-Bold.ttf .
  ln -s /usr/share/fonts/TTF/DejaVuSans-Bold.ttf VeraBd.ttf
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf .
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf Vera.ttf
  ln -s /usr/share/fonts/TTF/DejaVuSansCondensed.ttf .

}
